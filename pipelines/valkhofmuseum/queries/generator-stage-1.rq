PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX schema:  <https://schema.org/>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>
PREFIX crm:     <http://www.cidoc-crm.org/cidoc-crm/>

CONSTRUCT {
    $this a edm:ProvidedCHO ;
         edm:type ?edm_type ;
         dc:creator ?creator ;
         dc:description ?description ;
         dc:type ?genre ;
         dc:subject ?subject ;
         dc:title ?name ;
         dcterms:alternative ?altName ;
         dc:publisher ?publisher ;
         owl:sameAs ?sameAs ;
         dcterms:spatial ?spatial ;
         dcterms:temporal ?temporal ;
         dcterms:medium ?material .   

    ?uri_ore a ore:Aggregation ;
         edm:aggregatedCHO $this ;
         edm:provider ?provider ;
         edm:dataProvider ?dataProvider ;
         edm:rights ?license ;
         edm:isShownBy ?isShownBy ;
         edm:isShownAt ?isShownAt .
}
WHERE {

  # Generate an "URI_CHO#agg" URI pointing to a ore:Aggregation resource. 
  BIND( URI(CONCAT(STR($this),"#agg")) as ?uri_ore)

  # approach this collection as Europeana IMAGE collection
  BIND(("IMAGE") as ?edm_type)

  # DC4EU as fixed provider of the EDM dataset
  BIND(STR('DC4EU') as ?provider)

  # Valkhof Museum as the fixed data provider for this dataset
	BIND(("Valkhof Museum") as ?dataProvider)

  # the IRI of the object should resolve to a landingpage with the registration system
  BIND(IRI($this) as ?isShownAt) 

  # schema:license contains an invalid license which leads to problems with Europeana
  # original license link was: https://creativecommons.org/licenses/zero/1.0/
  # define a static license 
  BIND(IRI("http://creativecommons.org/publicdomain/zero/1.0/") as ?license)
  #OPTIONAL { $this schema:license ?cho_license }
  #BIND(COALESCE(IRI(?cho_license),IRI(?dataset_license),"no license info") as ?license)

  # a title or description is required by Europeana
  # require records with at least rdfs:label present 
  $this rdfs:label ?name .

  # Europeana requires valid URI for edm:isShownBy 
  # using the image link instead of the IIIF manifest for now
  $this schema:image ?image .
  BIND(IRI(?image) as ?isShownBy)

  # collect optional description fields
  OPTIONAL { $this schema:description ?description }
  
  # A ProvidedCHO must have a dc:subject or dc:type or dct:temporal or dct:spatial.
  OPTIONAL { $this schema:datePublished ?temporal}

  # this information is not available in the schema.org data so use the linked art data
  OPTIONAL { $this crm:P2_has_type/rdfs:label ?genre }

  # give it a try
  OPTIONAL { $this schema:keywords ?subject }
  OPTIONAL { $this schema:about ?subject }
  OPTIONAL { $this schema:spatial ?spatial }

#  OPTIONAL { $this schema:genre ?genre . 
#    FILTER (lang( ?genre) = "en")
#    BIND(str(?genre) as ?dc_genre)
#  }

  OPTIONAL { $this schema:creator ?creator }
  OPTIONAL { $this schema:alternateName ?altName }
  OPTIONAL { $this schema:publisher ?publisher }
  OPTIONAL { $this schema:sameAs ?sameAs }
  OPTIONAL { $this schema:material ?material }

}