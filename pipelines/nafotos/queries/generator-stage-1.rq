PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>
PREFIX nao:     <https://archief.nl/def/ontologie/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX odrl:    <http://www.w3.org/ns/odrl/2/>
PREFIX doap:    <http://usefulinc.com/ns/doap#>
PREFIX svcs:    <http://rdfs.org/sioc/services#>

CONSTRUCT {
	?uri_cho a edm:ProvidedCHO ;
		edm:type ?edm_type ;
		dc:creator ?creator ;
        dc:description ?description ;
        dc:type ?genre ;
        dc:subject ?subjecturi ;
        dc:subject ?subjectliteral ;
        dcterms:spatial ?spatial ;
        dcterms:medium ?material ;
		dc:rights ?policy ;
        dc:identifier ?identifier ;
        dc:date ?date ;
        dcterms:isPartOf ?is_part_of ;
		edm:pid ?pid ;
# 		dc:publisher ?publisher ;
	. 
	
	?uri_ore a ore:Aggregation ;
		edm:aggregatedCHO ?uri_cho ;
		edm:provider ?provider ;
		edm:dataProvider ?dataProvider ;
		edm:rights ?license ;
		edm:hasView ?isShownBy ; # was edm:isShownBy
		edm:isShownAt ?isShownAt ;
	.

	?isShownBy a edm:WebResource ;
		svcs:has_service ?uri_iiif ;
	.

	?uri_iiif a svcs:Service ;
		dcterms:conformsTo <http://iiif.io/api/image> ;
		doap:implements <http://iiif.io/api/image/2/level1.json> ;
	.

} WHERE {

	BIND( ("IMAGE") as ?edm_type )
	BIND( ("Foto") as ?genre )
	BIND( STRLANG("DC4EU - Dutch Collections for Europe", "en") AS ?provider )
	BIND( STRLANG("Nationaal Archief","nl") as ?dataProvider )
	BIND( IRI("http://creativecommons.org/publicdomain/mark/1.0/") as ?policy )

	?this ore:aggregates ?uri_cho .
#	?uri_cho a nao:Archiefeenheid .

	?uri_ore foaf:primaryTopic ?uri_cho .
	?uri_ore odrl:hasPolicy <https://archief.nl/def/Policy/set_b_rechtenvrij__publiek_domein> . 

	?this dc:identifier ?identifier ;
		  dcterms:creator/skos:prefLabel ?_creator . 
	BIND(STRLANG(?_creator, "nl") AS ?creator)
	
	?this edm:isShownAt ?isShownAt .
	?this edm:hasView ?Zichtexemplaar .
	?Zichtexemplaar nao:webserviceIIIF ?iiif_info .

	BIND( IRI(CONCAT(REPLACE(STR(?iiif_info),"info.json","full/max/0/default.jpg"))) as ?isShownBy) .
	BIND( IRI(CONCAT(REPLACE(STR(?iiif_info),"info.json",""))) as ?uri_iiif) .
	
	OPTIONAL { 
		?uri_cho nao:isMateriaaltype ?_material .  
		BIND(STRLANG(?_material, "nl") AS ?material)
	}

	# TODO: onderzoeken wat er gebeurt bij 2 ?descriptions
	
	OPTIONAL {
		?uri_ore dc:description ?_description . 	
		BIND(STRLANG(?_description, "nl") AS ?description)  
	}
	OPTIONAL { 
		?uri_ore nao:toelichting ?_description .  	
		BIND(STRLANG(?_description, "nl") AS ?description) 
	}

	OPTIONAL { 
		?uri_ore dcterms:subject ?subjecturi . 
		?subjecturi skos:prefLabel ?_preflabel . 
		BIND(STRLANG(?_preflabel, "nl") AS ?preflabel)
	}
	
	OPTIONAL { 
		?uri_ore nao:trefwoordAlgemeen ?_subjectliteral . 
		BIND(STRLANG(?_subjectliteral, "nl") AS ?subjectliteral)  
	}
	OPTIONAL { 
		?uri_ore nao:trefwoordLocatie ?_spatial . 
		BIND(STRLANG(?_spatial, "nl") AS ?spatial) 
	}
	OPTIONAL { ?uri_ore dcterms:date ?date  }
	
	# TODO: onderzoeken wat er gebeurt bij 2 ?is_part_of
	
	OPTIONAL { 
		?uri_ore nao:titelFotocollectie ?_is_part_of . 
		BIND(STRLANG(?_is_part_of, "nl") AS ?is_part_of) 
	}
	
	OPTIONAL { 
		?uri_ore nao:titelFotoserie ?_is_part_of . 
		BIND(STRLANG(?_is_part_of, "nl") AS ?is_part_of) 
	}

	# voorkomende waarden:
    # "Nationaal Archief, CC0"@nl > http://creativecommons.org/publicdomain/zero/1.0/
	# "Nationaal Archief, Public Domain"@nl > http://creativecommons.org/publicdomain/mark/1.0/

	OPTIONAL { 
		?uri_ore odrl:hasPolicy/dcterms:rightsHolder ?_license . 
		BIND(
		  IF(?_license = "Nationaal Archief, CC0", <http://creativecommons.org/publicdomain/zero/1.0/>,
			IF(?_license = "Nationaal Archief, Public Domain", <http://creativecommons.org/publicdomain/mark/1.0/>, "????")
		  ) AS ?license
		)
	}
	
} LIMIT 800
